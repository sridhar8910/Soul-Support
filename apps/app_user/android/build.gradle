allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://storage.googleapis.com/download.flutter.io" }
    }
}

subprojects {
    configurations.all {
        resolutionStrategy {
            force("com.google.errorprone:error_prone_annotations:2.18.0")
        }
    }
}

rootProject.buildDir = "../build"
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.evaluationDependsOn(":app")
}

subprojects { subproject ->
    if (subproject == project) {
        return
    }
    subproject.plugins.whenPluginAdded {
        if (subproject.extensions.findByName("android")?.hasProperty("namespace") == true) {
            subproject.afterEvaluate {
                def androidExt = subproject.extensions.findByName("android")
                def currentNamespace = androidExt.namespace
                if (currentNamespace == null || currentNamespace.toString().trim().isEmpty()) {
                    def fallback = subproject.group
                    if (fallback == null || fallback.toString().trim().isEmpty()) {
                        fallback = "com.generated.${subproject.name.replace('-', '_')}"
                    }
                    androidExt.namespace = fallback.toString()
                }
            }
        }
    }
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
